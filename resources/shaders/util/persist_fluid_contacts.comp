const float positive_infinity = uintBitsToFloat(0x7F800000);
const float negative_infinity = uintBitsToFloat(0xFF800000);

layout(shared, binding = FLUID_CONTACT_COUNT_BINDING) uniform Fluid_Contact_Count
{
	//uint persistent_count;
	uint count;
} fluid_contact_count;

struct Fluid_Contact
{
	bool inactive;
	float impulse;
	uvec2 particles;
	vec2 direction;
	float target_velocity;
	float mass;
	vec2 impulse_range;
};

layout(shared, binding = FLUID_CONTACT_BINDING) restrict
buffer Fluid_Contacts
{
	Fluid_Contact contacts[MAX_FLUID_CONTACT_COUNT];
} fluid_contacts;

layout(shared, binding = FLUID_POSITION_BINDING) restrict readonly
buffer Fluid_Position
{
	ivec2 p[MAX_FLUID_PARTICLE_COUNT];
} fluid_position;

layout(shared, binding = FLUID_VELOCITY_BINDING) restrict readonly
buffer Fluid_Velocity
{
	ivec2 v[MAX_FLUID_PARTICLE_COUNT];
} fluid_velocity;

layout(local_size_x = LOCAL_SIZE) in;
void main()
{
	if (gl_GlobalInvocationID.x >= fluid_contact_count.count)
	{
		return;
	}

	uvec2 particles = fluid_contacts.contacts[gl_GlobalInvocationID.x].particles;
	
	// TODO: Fetch from buffer
	float radius_0 = RADIUS;
	float radius_1 = RADIUS;

	ivec2 position_0 = fluid_position.p[particles.x];
	ivec2 position_1 = fluid_position.p[particles.y];

	vec2 direction = vec2(position_0 - position_1);	// TODO: Conversion to vec2 unnecessary???
	float distance = length(direction);
	float max_distance = radius_0 + radius_1;
	if (distance >= max_distance)
	{
		fluid_contacts.contacts[gl_GlobalInvocationID.x].inactive = true;
	}
	else
	{
		bool was_inactive = fluid_contacts.contacts[gl_GlobalInvocationID.x].inactive;

		// TODO: Handle distance being 0
		direction /= distance;		

		float target_velocity = 0.002 * ((0.05 * (METER)) - (max_distance - distance));

		if (was_inactive)
		{
			ivec2 velocity_0 = fluid_velocity.v[particles.x];
			ivec2 velocity_1 = fluid_velocity.v[particles.y];
			target_velocity = 1.0 * min(0.0, dot(direction, vec2(velocity_0 - velocity_1))); // Is min necessary?

			fluid_contacts.contacts[gl_GlobalInvocationID.x].inactive = false;
			// IMPORTANT TODO: COMPUTE (SCALED) INITIAL IMPULSE BASED ON VELOCITY FOR FASTER CONVERGENCE
			fluid_contacts.contacts[gl_GlobalInvocationID.x].impulse = 0.0;
		}

		fluid_contacts.contacts[gl_GlobalInvocationID.x].direction = direction;
		fluid_contacts.contacts[gl_GlobalInvocationID.x].target_velocity = target_velocity;
		fluid_contacts.contacts[gl_GlobalInvocationID.x].impulse_range = vec2(negative_infinity, 0.0);
	}
}
