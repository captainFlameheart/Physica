layout(shared, binding = FLUID_CONTACT_COUNT_BINDING) uniform Fluid_Contact_Count
{
	//uint persistent_count;
	uint count;
} fluid_contact_count;

struct Fluid_Contact
{
	bool inactive;
	float impulse;
	uvec2 particles;
	vec2 direction;
	float target_velocity;
	float mass;
	vec2 impulse_range;
};

layout(shared, binding = FLUID_CONTACT_BINDING) restrict readonly
buffer Fluid_Contacts
{
	Fluid_Contact contacts[MAX_FLUID_CONTACT_COUNT];
} fluid_contacts;

layout(shared, binding = FLUID_VELOCITY_BINDING) restrict
buffer Fluid_Velocity
{
	//ivec2 v[MAX_FLUID_PARTICLE_COUNT];
	ivec4 v[MAX_FLUID_PARTICLE_COUNT];
} fluid_velocity;

layout(local_size_x = LOCAL_SIZE) in;
void main()
{
	// TODO: Apply warm start impulses to ALL triangle contacts as well (we should initialize new contact impulses 
	// to non-zero for faster convergence).
	if (gl_GlobalInvocationID.x >= fluid_contact_count.count || fluid_contacts.contacts[gl_GlobalInvocationID.x].inactive)
	{
		return;
	}

	float impulse = fluid_contacts.contacts[gl_GlobalInvocationID.x].impulse;
	uvec2 particles = fluid_contacts.contacts[gl_GlobalInvocationID.x].particles;
	vec2 direction = fluid_contacts.contacts[gl_GlobalInvocationID.x].direction;
	
	// TODO: Fetch masses from buffer
	float inverse_mass_0 = INVERSE_MASS;
	float inverse_mass_1 = INVERSE_MASS;

	vec2 impulse_vector = impulse * direction;

	ivec2 delta_velocity_0 = ivec2(-inverse_mass_0 * impulse_vector);
	ivec2 delta_velocity_1 = ivec2(inverse_mass_1 * impulse_vector);

	atomicAdd(fluid_velocity.v[particles.x].x, delta_velocity_0.x);
	atomicAdd(fluid_velocity.v[particles.x].y, delta_velocity_0.y);

	atomicAdd(fluid_velocity.v[particles.y].x, delta_velocity_1.x);
	atomicAdd(fluid_velocity.v[particles.y].y, delta_velocity_1.y);
}
